apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.jpventura.anypic"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 0
        versionName "0.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        debug {
            keyAlias 'debug'
            keyPassword '4Fb9VpE4aeZGp67s'
            storeFile file('android.jks')
            storePassword 'fY2ndYbSYc8msJzV'
        }
        release {
            keyAlias 'release'
            keyPassword 'Fk296fZYXHeT435x'
            storeFile file('android.jks')
            storePassword 'fY2ndYbSYc8msJzV'
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    buildTypes.each {
        it.resValue 'string', 'content_authority', CONTENT_AUTHORITY
        it.resValue 'string', 'facebook_app_id', FACEBOOK_APP_ID
        it.resValue 'string', 'fb_login_protocol_scheme', FB_LOGIN_PROTOCOL_SCHEME
    }
}

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.22.2'
    }
}

crashlytics {
    enableNdk true
    androidNdkOut 'src/main/obj'
    androidNdkLibsOut 'src/main/libs'
}

dependencies {
    implementation "com.android.support.constraint:constraint-layout:$rootProject.constrainLayoutVersion"
    implementation "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:design:$rootProject.supportLibraryVersion"
    implementation "com.android.support:support-vector-drawable:$rootProject.supportLibraryVersion"
    implementation "com.facebook.android:facebook-android-sdk:4.23.0"
    implementation "com.google.code.gson:gson:2.8.1"
    implementation "com.google.firebase:firebase-auth:$rootProject.googleFirebaseVersion"
    implementation "com.google.firebase:firebase-core:$rootProject.googleFirebaseVersion"
    implementation "com.google.guava:guava:$rootProject.guavaVersion"
    implementation "com.jakewharton.timber:timber:$rootProject.timberVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.rxAndroidVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rootProject.rxJavaVersion"
    implementation project(':jpventura')
    implementation fileTree(dir: "libs", include: ["*.jar"])
    debugImplementation "com.facebook.stetho:stetho:$rootProject.stethoVersion"
    releaseImplementation("com.crashlytics.sdk.android:answers:$rootProject.answersVersion@aar") {
        transitive = true
    }
    releaseImplementation("com.crashlytics.sdk.android:crashlytics:$rootProject.crashlyticsVersion@aar") {
        transitive = true
    }
    releaseImplementation("com.crashlytics.sdk.android:crashlytics-ndk:$rootProject.crashlyticsNdkVersion@aar") {
        transitive = true
    }
    releaseImplementation "com.google.firebase:firebase-crash:$rootProject.googleFirebaseVersion"

    androidTestImplementation("com.android.support.test.espresso:espresso-core:$rootProject.espressoVersion", {
        exclude group: "com.android.support", module: "support-annotations"
    })

    testImplementation "junit:junit:$rootProject.junitVersion"
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.google.gms.google-services'